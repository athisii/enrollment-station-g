plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

group 'com.cdac'
version '1.4'

repositories {
    mavenLocal()
    mavenCentral()
}


def junitVersion = '5.9.1'
def lombokVersion = '1.18.24'
def jarDir = '/usr/share/enrollment/lib/'

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.cdac.enrollmentstation'
    mainClass = 'com.cdac.enrollmentstation.App'
}
dependencies {
    implementation fileTree(dir: 'libs', include: '*.jar')
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.0'
    implementation 'net.java.dev.jna:jna:4.2.2' // used by iengine
    implementation 'com.twelvemonkeys.imageio:imageio-jpeg:3.9.4'

    implementation 'org.bouncycastle:bcprov-jdk15on:1.60' // latest version not supported

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}


javafx {
    version = '17.0.10'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.swing']
}

jar {
    manifest {
        attributes("Main-Class": "com.cdac.enrollmentstation.App")
        attributes("Class-Path": configurations.runtimeClasspath.files.collect { jarDir + it.getName() }.join(' '))
    }
}


test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'enrollment-station'
    }
}

jlinkZip {
    group = 'distribution'
}

// custom Task
task copyDependencies(type: Copy) {
    group "copy dependencies"
    description "Copy dependencies to ${jarDir}"
    configurations.compileClasspath.files.collect {
        it.getAbsolutePath()
    }.forEach {
        from(it)
        into jarDir
    }
}
