plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

group 'com.cdac'
version '1.3'

repositories {
    mavenLocal()
    mavenCentral()
}


def junitVersion = '5.9.1'
def lombokVersion = '1.18.24'
def jarDir = '/usr/share/enrollment/lib/'

sourceCompatibility = '11'
targetCompatibility = '11'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.cdac.enrollmentstation'
    mainClass = 'com.cdac.enrollmentstation.App'
}
dependencies {
    implementation fileTree(dir: 'libs', include: '*.jar')
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.1'
    implementation 'org.scream3r:jssc:2.8.0'
    implementation 'commons-io:commons-io:2.8.0'
    implementation 'net.java.dev.jna:jna:4.2.2'

    implementation 'com.cedarsoftware:json-io:4.12.0'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.60' // latest version not supported
    implementation 'javax.xml.bind:jaxb-api:2.3.1' // needs to be improved
    implementation 'org.json:json:20200518'

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")

//    implementation 'org.json:json:20160810'
//    implementation 'org.bouncycastle:bcprov-jdk15on:1.69'
//    implementation 'io.opentracing:opentracing-api:0.33.0'
//    implementation 'com.google.code.gson:gson:2.8.5'
//    implementation 'commons-codec:commons-codec:1.15' // transitively imported by MFS jar
//    implementation 'io.rest-assured:rest-assured-all:4.2.0'


//    implementation 'org.skyscreamer:jsonassert:1.5.0'
//    testImplementation('com.opera:operadriver:1.5') {
//        exclude group: 'org.seleniumhq.selenium', module: 'selenium-java'
//    }
//    testImplementation 'org.seleniumhq.selenium:selenium-java:4.7.1'

}


javafx {
//    version = '13'
    version = '11.0.2'
    modules = ['javafx.controls', 'javafx.fxml']
}

jar {
    manifest {
        attributes("Main-Class": "com.cdac.enrollmentstation.App")
        attributes("Class-Path": configurations.runtimeClasspath.files.collect { jarDir + it.getName() }.join(' '))
    }
}


test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'enrollment-station'
    }
}

jlinkZip {
    group = 'distribution'
}

// custom Task
task copyDependencies(type: Copy) {
    group "copy dependencies"
    description "Copy dependencies to ${jarDir}"
    configurations.runtimeClasspath.files.collect {
        it.getAbsolutePath()
    }.forEach {
        from(it)
        into jarDir
    }
}
